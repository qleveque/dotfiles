#!/usr/bin/zsh
stty -ixon
setopt histignorealldups sharehistory nonomatch
bindkey -v
bindkey '^[[Z' forward-word
local reset-prompt() {zle reset-prompt && zle autosuggest-clear && zle zle-line-init}
export PATH="$HOME/.bin:$HOME/bin:$HOME/.local/bin:$PATH"
export EDITOR='/usr/bin/nvim'
alias e=$EDITOR
source ~/.aliases
source ~/.colors

export DIR_HIST=~/.dirhist
export FZF_ALT_C_COMMAND='fdfind --type d'
export FZF_CTRL_T_COMMAND='fdfind'
export FZF_DEFAULT_COMMAND='fdfind'
export FZF_DEFAULT_OPTS='--ansi --height 40% --border'
export FZF_P_COMMAND='eval "tac $DIR_HIST | grep -v \"^$PWD\$\""'
export JUMP_TARGET='readlink "$MARKPATH/$1"'
export LESS='-irFRX'

HISTFILE=~/.zsh_history
HISTSIZE=1000
KEYTIMEOUT=20
PROMPT=$'\n%B%K{bold,white}%F{cyan}%S%n@%M%s%f%k%b %B%F{blue}%3~%f%b %# '
RPS1=Â 
SAVEHIST=1000
VI_MODE_SET_CURSOR=true
ZSH_AUTOSUGGEST_CLEAR_WIDGETS=(vi-cmd-mode accept-line push-line-or-edit)
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=$LIGHTEST_BG"
ZSH_AUTOSUGGEST_MANUAL_REBIND="true"

# Sources
source /usr/share/doc/fzf/examples/completion.zsh
source /usr/share/doc/fzf/examples/key-bindings.zsh
source ~/.vim/plugged/vim-tmux-navigator/vim-tmux-navigator.tmux

# Plugins
source ~/.zplug/init.zsh
zplug "Aloxaf/fzf-tab"
zplug "kutsan/zsh-system-clipboard"
zplug "plugins/extract", from:oh-my-zsh
zplug "plugins/jump", from:oh-my-zsh
zplug "plugins/vi-mode", from:oh-my-zsh
zplug "zsh-users/zsh-autosuggestions"
zplug "zsh-users/zsh-syntax-highlighting", defer:2
zplug load

# Ctrl-F
zle-vifm-only-choose-dir() {
  local dst=$(vifm -c :only --choose-dir - . < /dev/tty)
  if [ -z "$dst" ]; then
    echo 'Directory picking cancelled/failed'
    return 1
  fi
  cd "$dst"
  reset-prompt
}
zle -N zle-vifm-only-choose-dir && bindkey '^f' zle-vifm-only-choose-dir

# Ctrl-P
autoload -U add-zsh-hook
add-zsh-hook chpwd _stack_new_dir
_stack_new_dir() {
  touch $DIR_HIST
  sed -i "\#^$PWD\$#d" $DIR_HIST
  echo "$(cat $DIR_HIST | tail -299)\n$PWD" > $DIR_HIST
}
zle-recent-directory() {
  LBUFFER+="`eval $FZF_P_COMMAND | fzf --exact`"
  reset-prompt
}
zle-go-to-recent-directory() {
  cd "`eval $FZF_P_COMMAND | fzf --exact`"
  reset-prompt
}
zle -N zle-recent-directory && bindkey '^P' zle-recent-directory
zle -N zle-go-to-recent-directory && bindkey '\ep' zle-go-to-recent-directory

# Ctrl-V
zle-clipboard-paste() {
  LBUFFER+="$(xclip -selection clipboard -out)"
  reset-prompt
}
zle -N zle-clipboard-paste && bindkey '^V' zle-clipboard-paste

# Ctrl-Q
zle-exit-zsh() {exit}
zle -N zle-exit-zsh && bindkey '^Q' zle-exit-zsh && bindkey -M vicmd '^Q' zle-exit-zsh

# Windows
if $(grep -q Microsoft /proc/version); then
  export DISPLAY=:0.0
  cmd() {cmd.exe /C $@}
  for filename in ~/bin/*.bat; do
    alias "${filename:t:r}=cmd.exe /C %USERPROFILE%/bin/${filename:t:r}.bat"
  done
  for filename in ~/bin/*.ps1; do
    alias "${filename:t:r}=powershell.exe -c \~/bin/${filename:t:r}.ps1"
  done
fi
